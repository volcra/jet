apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'idea'
apply plugin: 'maven'

version = '0.1.2-SNAPSHOT'
mainClassName = 'org.volcra.jet.cli.Main'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    all*.exclude module: 'commons-logging'
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.4:indy'
    compile 'org.eclipse.jetty:jetty-server:9.2.1.v20140609'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'commons-cli:commons-cli:1.2'

    runtime 'org.slf4j:jcl-over-slf4j:1.7.7'
    runtime 'org.slf4j:slf4j-jdk14:1.7.7'

    testCompile 'junit:junit:4.11'
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task jet(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'org.volcra.jet.cli.Main'
}

assemble.dependsOn installApp

artifacts {
    archives sourcesJar
    archives javadocJar
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}